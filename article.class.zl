class Article
	// 显示文章内容
	fun show(static = FALSE)
		global querys, categories, db, config;
		id = bltInt(querys['id']);
		data = Mysql.fetchOne(db, "select * from article where id='"+id+"'");
		common_sql = "'" + id + "' and cid='"+ data['cid'] +"' order by ";
		prev_article = Mysql.fetchOne(db, "select id,title,DATE_FORMAT(created_at, '%Y%m') as format_created from article where id < " + common_sql + " id desc limit 0,1");
		if(bltCount(prev_article) > 0)
			data['prev_article'] = prev_article;
		endif
		next_article = Mysql.fetchOne(db, "select id,title,DATE_FORMAT(created_at, '%Y%m') as format_created from article where id > " + common_sql + " id asc limit 0,1");
		if(bltCount(next_article) > 0)
			data['next_article'] = next_article;
		endif
		rand_article = Mysql.fetchAll(db, "select id,title,DATE_FORMAT(created_at, '%Y%m') as format_created from article where id != " + common_sql + " RAND() limit 6");
		if(bltCount(rand_article) > 0)
			data['rand_article'] = rand_article;
		endif
		data['csses'] = bltArray('article-show.css?v=20181020');
		data['head_js'] = bltArray('jquery.yestop.js');
		data['static'] = static;
		Article.SetCurCate(categories, data, data['cid']);
		data['show_comment'] = config['comment'];
		content = bltMustacheFileRender("/tpl/article_show.tpl", data);
		if(!static)
			print content;
		else
			return content;
		endif
	endfun

	// 显示文章列表
	fun list(static = FALSE)
		global querys, categories, db, config;
		cid = bltInt(querys['cid']);
		where = " where cid=" + cid;
		tmp = Mysql.fetchOne(db, "select count(1) as cnt from article " + where);
		total = bltInt(tmp[cnt]);
		page = get_page(config['list_page_count'], total, 10);
		if(static)
			page['link'] = '/c/' + cid + '/';
			page['prev_link'] = page['link'] + ((page['prev'] == 1) ? '' : page['prev'] + '.html');
			page['next_link'] = page['link'] + ((page['next'] == 1) ? '' : page['next'] + '.html');
			page['totalpage_link'] = page['link'] + ((page['totalpage'] == 1) ? '' : page['totalpage'] + '.html');
		endif
		for(i=page['start']; i <= page['end'];i++)
			cur = (page['curpage'] == i) ? ' class="active"' : '';
			if(!static)
				pages[] = '<li'+cur+'><a href="'+page['link']+'page='+i+'">'+i+'</a></li>';
			else
				page_html = page['link'] + ((i==1) ? '' : i + '.html');
				pages[] = '<li'+cur+'><a href="' + page_html + '">'+i+'</a></li>';
			endif
		endfor
		data['head_js'] = bltArray('jquery.yestop.js');
		data['static'] = static;
		data['page'] = page;
		data['pages'] = pages;
		data['articles'] = Mysql.fetchAll(db, "select id,title,thumbnail,description,created_at,DATE_FORMAT(created_at, '%Y%m') as format_created from article "+ where +
							" order by id desc limit " + page['offset'] + "," + page['limit']);
		Article.SetCurCate(categories, data, cid);
		content = bltMustacheFileRender("/tpl/article_list.tpl", data);
		if(!static)
			print content;
		else
			return content;
		endif
	endfun

	// 设置当前分类
	fun SetCurCate(categories, data, cid)
		global config;
		for(i=0;bltIterArray(categories,&i,&v);)
			if(v['id'] == cid)
				v['cur'] = 'yes';
				if(!data['title'])
					data['title'] = v['name'];
					data['description'] = v['description'];
				endif
				data['catname'] = v['name'];
				break;
			endif
		endfor
		data['site_name'] = config['site_name'];
		data['head_title'] = data['title'] + ' - ' + config['site_name'];
		data['categories'] = categories;
	endfun
endclass
