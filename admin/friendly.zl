inc 'common.zl';
inc 'helper.zl';

querys = rqtGetQuery();
action = querys['act'] ? querys['act'] : 'list';
if(action == 'list')
	Friendly.list();
elif(action == 'add')
	Friendly.add();
elif(action == 'edit')
	Friendly.edit();
elif(action == 'delete')
	Friendly.delete();
else
	print 'invalid act';
endif

class Friendly
	// 显示友情链接列表
	fun list()
		global menus, querys;
		data['title'] = '友情链接:';
		setCurMenu(menus, 'friendly.zl');
		data['menus'] = menus;
		db = Friendly.initDB();
		tmp = Mysql.fetchOne(db, "select count(1) as cnt from friendly_link ");
		total = bltInt(tmp['cnt']);
		page = get_page(10, total, 10);
		for(i=page['start']; i <= page['end'];i++)
			cur = (page['curpage'] == i) ? ' class="active"' : '';
			pages[] = '<li'+cur+'><a href="'+page['link']+'page='+i+'">'+i+'</a></li>';
		endfor
		data['page'] = page;
		data['pages'] = pages;
		data['items'] = Mysql.fetchAll(db, "select * from friendly_link order by id desc limit " + page['offset'] + "," + page['limit']);
		print bltMustacheFileRender("tpl/friendly_list.tpl", data);
	endfun

	// 初始化数据库连接
	fun initDB()
		global config;
		db = bltArray();
		Mysql.init(db, config, "tpl/error.tpl");
		return db;
	endfun

	// 显示添加或编辑公告的页面
	fun showAdd(db, data, isadd = TRUE)
		global menus;
		setCurMenu(menus, 'friendly.zl');
		csses = bltArray('jquery.fileupload.css');
		js = bltArray('jquery.ui.widget.js', 'jquery.iframe-transport.js', 'jquery.fileupload.js');
		data['csses'] = csses;
		data['head_js'] = js;
		data['menus'] = menus;
		data['title'] = isadd ? '添加友情链接' : '编辑友情链接';
		data['act'] = isadd ? 'add' : 'edit';
		print bltMustacheFileRender("tpl/friendly_add.tpl", data);
	endfun

	// 校验数据
	fun validate(db, posts, isadd = TRUE)
		bltUnset(&posts['thumbnail_up']);
		bltStr(&posts['name'], TRUE);
		bltStr(&posts['url'], TRUE);
		bltStr(&posts['logo'], TRUE);
		bltStr(&posts['description'], TRUE);
		if(!posts['name'])
			data['err_msg'] = '网站名称不能为空';
		elif(!posts['url'])
			data['err_msg'] = '网站url不能为空';
		elif(!isadd && !posts['id'])
			data['err_msg'] = '无效的ID';
		endif
		if(data['err_msg'])
			data['posts'] = posts;
			if(posts['id'])
				data['id'] = posts['id'];
			endif
			Friendly.showAdd(db, data, isadd);
			bltExit();
		endif
	endfun

	// 添加友情链接
	fun add()
		posts = rqtGetBodyAsArray();
		db = Friendly.initDB();
		if(posts['submit'])
			Friendly.validate(db, posts);
			bltUnset(&posts['submit']);
			posts['created_at'] = bltDate('%Y-%m-%d %H:%M:%S');
			posts['updated_at'] = posts['created_at'];
			Mysql.Insert(db, 'friendly_link', posts);
			data['success_msg'] = '添加友情链接成功';
		endif
		Friendly.showAdd(db, data);
	endfun

	// 编辑友情链接
	fun edit()
		global querys;
		posts = rqtGetBodyAsArray();
		db = Friendly.initDB();
		if(posts['submit'])
			id = bltInt(posts['id']);
			Friendly.validate(db, posts);
			bltUnset(&posts['submit']);
			posts['updated_at'] = bltDate('%Y-%m-%d %H:%M:%S');
			Mysql.Update(db, 'friendly_link', posts, 'id=' + id);
			data['success_msg'] = '编辑友情链接成功';
		else
			id = bltInt(querys['id']);
		endif
		data['posts'] = Mysql.fetchOne(db, "select * from friendly_link where id='"+id+"'");
		if(!bltCount(data['posts']))
			data['err_msg'] = '无效的ID';
			id = 0;
		endif
		data['id'] = id;
		Friendly.showAdd(db, data, FALSE);
	endfun

	// 删除友情链接
	fun delete()
		global querys;
		db = Friendly.initDB();
		id = querys['id'] ? bltInt(querys['id']) : 0;
		if(id <= 0) 
			ajax_return('无效的id');
		endif
		Mysql.Exec(db, "DELETE FROM friendly_link WHERE id="+id);
		ajax_return();
	endfun
endclass
